import com.github.spotbugs.snom.Effort

plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'com.github.spotbugs' version '6.4.8'
    id "dev.yumi.gradle.licenser" version "2.2.2"
    id "com.github.ben-manes.versions" version "0.53.0"
    id 'maven-publish'
    id "com.vanniktech.maven.publish" version "0.36.0"
    id 'signing'
}

group = 'io.github.sgzachesov'
version = '4.0.2'
description = "spring-data-jpa-specification-builder is an auxiliary library for building the Spring Specification."

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    pom {
        name.set(project.name)
        description.set(project.provider(project::getDescription))
        inceptionYear.set("2025")
        url.set("https://github.com/sgzachesov/spring-data-jpa-specification-builder")
        licenses {
            license {
                name.set("Apache License Version 2.0")
                url.set("https://www.apache.org/licenses/LICENSE-2.0")
                distribution.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
            }
        }
        developers {
            developer {
                id.set("sgzachesov")
                name.set("Sergei Zachesov")
                email.set("szachesov@yandex.ru")
            }
        }
        scm {
            connection.set("scm:git:https://github.com/sgzachesov/spring-data-jpa-specification-builder.git")
            developerConnection.set("scm:git@github.com:sgzachesov/spring-data-jpa-specification-builder.git")
            url.set("https://github.com/sgzachesov/spring-data-jpa-specification-builder")
        }
    }
}

signing {
    if (!version.toString().endsWith("SNAPSHOT")) {
        useGpgCmd()
        sign(publishing.publications)
    }
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext {
    springVersion = '4.0.2'
    lombokVersion = '1.18.42'
    testcontainersVersion = '1.21.4'
    hibernateVersion = '7.2.4.Final'
}

dependencies {

    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springVersion}"
    implementation 'org.jspecify:jspecify:1.0.0'

    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher:6.0.3"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springVersion}"
    implementation "org.springframework.boot:spring-boot-data-jpa-test:${springVersion}"
    testImplementation "org.springframework.boot:spring-boot-testcontainers:${springVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"
    testRuntimeOnly 'org.postgresql:postgresql:42.7.10'
    testCompileOnly "org.hibernate.orm:hibernate-jpamodelgen:${hibernateVersion}"
    testAnnotationProcessor "org.hibernate.orm:hibernate-jpamodelgen:${hibernateVersion}"
    testImplementation 'com.cosium.spring.data:spring-data-jpa-entity-graph:4.0.2'
}

test {
    useJUnitPlatform()
    //https://stackoverflow.com/a/78188896/17965846
    jvmArgs("-XX:+EnableDynamicAgentLoading")
    dependsOn checkstyleMain, checkstyleTest
    finalizedBy jacocoTestCoverageVerification
    finalizedBy jacocoTestReport
}

checkstyle {
        toolVersion = '13.2.0'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0

    configProperties = [
            'configDirectory': rootDir.toString() + "/config/checkstyle"
    ]
}
checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}

license {
    rule(file("LICENSE-HEADER"))
    include("**/*.java")
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    checkForGradleUpdate = true
    gradleReleaseChannel = "current"
    checkConstraints = true
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

pmd {
    toolVersion = "7.17.0"
    consoleOutput = true
    ruleSetFiles = files("${rootDir}/config/pmd/pmd.xml")
    ruleSets = []
}

spotbugs {
    showProgress = true
    effort = Effort.MAX
    reportLevel = 'low' //Confidence.LOW - Access to 'LOW' exceeds its access rights
    excludeFilter = file("${rootDir}/config/spotbugs/exclude.xml")
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    dependsOn(test)
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn(jacocoTestReport)
    violationRules {
        rule {
            limit {
                counter = "CLASS"
                value = "MISSEDCOUNT"
                maximum = "0.0".toBigDecimal()
            }
        }
        rule {
            limit {
                counter = "METHOD"
                value = "MISSEDCOUNT"
                maximum = "0.0".toBigDecimal()
            }
        }
        rule {
            limit {
                counter = "LINE"
                value = "MISSEDCOUNT"
                maximum = "0.0".toBigDecimal()
            }
        }
        rule {
            limit {
                counter = "INSTRUCTION"
                value = "COVEREDRATIO"
                minimum = "1.0".toBigDecimal()
            }
        }
        rule {
            limit {
                counter = "BRANCH"
                value = "COVEREDRATIO"
                minimum = "0.9".toBigDecimal()
            }
        }
    }
}


tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}